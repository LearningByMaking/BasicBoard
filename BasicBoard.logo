;jlogo file for BasicBoard
 
 to BasicBoard
 	print "|starting BasicBoard|
 	init-BasicBoard
 	loop
 	[
 		see-packet
 		wait 150
 	]
 end
 
 to init-BasicBoard
  init-hardware 
 	make "check 0
  make "oldcheck 0
  make "time-list []
  make "light-list []
  make "start-time int ( now / 1000 ) 
  make "firstrun 1   

 end
 
 to init-hardware
 	stopvm
 	ul-ready
 	wait 15
 	ul-set
 	wait 15
 	ul-go
 	wait 15

 end

 to see-packet
  make "packet-bytes rl $1fc0 30   
  if (count :packet-bytes) > 0 
	[
    process-packet             
	]
    
end

to process-packet	
	process-header  
end

to process-header
  let [th 0]
  let [tl 0]
  make "oldcheck :check
  let [nws ((first :packet-bytes) / 2) + 1]
  make "check wnth :nws :packet-bytes  
  if not (:check = :oldcheck) 
  [
    make "type nth 1 :packet-bytes
    make "wp packet-words :packet-bytes       
    make "th wnth 2 :packet-bytes         
    make "tl wnth 3 :packet-bytes        
    if (:type = 42) [ process-data-packet ]
  ]
end

to process-data-packet
	make "light wnth 4 :packet-bytes
	make "temp1 wnth 5 :packet-bytes
	make "temp2 wnth 6 :packet-bytes
  make "time elapsedTime
	print (se "|light:| :light "|temp1:| :temp1 "|temp2:| :temp2 "|time:| :time)
  addTimeToList :time
  addLightToList :light 
  plot-light
end

to plot-light
   x-data "|Elapsed Time (seconds)| :time-list
   y-data "|Light (adc)| :light-list 
   plot 2
   if(:firstrun = 1)
   [
      display 
   ]
  
end

to packet-words :l          
  let [res []]
  let [nws (first :l) / 2 + 2]
  if ( :nws > 25 ) [ make "nws 25 ]
  dotimes [i :nws - 1][make "res se :res bf 100000 + wnth :i :l]
  output :res
end

to wnth :i :l
  output (nth 2 * :i :l) + 256 * (nth 2 * :i + 1 :l)
end

to elapsedTime
   output  int ( now / 1000 ) - :start-time
end

to addTimeToList :time
  make "time-list (se :time-list :time)
end

to addLightToList :photo
  make "light-list (se :light-list :photo)
end

to x-data :n :m
  make "xname :n
  make "xcol-data :m
end

to y-data :n :m
  make "yname :n
  make "ycol-data :m
end

to plot :s
  setup-screen
  clean
  select-all
  limits xlower xupper ylower yupper 
  grid 10 10
  box
  xaxis 10  1
  yaxis 10  1

  if :s = 0 [ histogram ]
  if :s = 1 [ connect ]
  if :s = 2 [ ptype 2 points ]

  title (se :yname "versus :xname)
  xlabel :xname
  redraw

end

to display  
  blindexec "|display -update 1 screen.png | 
  make "firstrun 0 
end



