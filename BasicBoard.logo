;jlogo file for BasicBoard
 
 to BasicBoard
 	print "|starting BasicBoard|
 	init-BasicBoard
 	loop
 	[
 		see-packet
 		wait 150
 	]
 end
 
 to init-BasicBoard
  init-hardware 
 	make "check 0
  make "oldcheck 0
  make "time-list []
  make "light-list []
  make "start-time int ( now / 1000 ) ;accurate to within 1 second
  make "firstrun 1   

 end
 
 to init-hardware
 	stopvm ;stop vm makes sure the board is not running
 	ul-power
 	wait 15
 	ul-init
 	wait 15
 	ul-go
 	wait 15
 end 

 
 to see-packet
  make "packet-bytes retrieve-packet   ;reads the packet off the one-wire network from the app board
  if (count :packet-bytes) > 0 
	[
		process-header            
	]
    
end


to process-header

  make "oldcheck :check 
  let [nws ((first :packet-bytes) / 2) + 1] 
  make "check wnth :nws :packet-bytes ;checksum is the last byte in the packet
  if not (:check = :oldcheck) ;if checksums are equal, the data is NOT new
  [
    make "type nth 1 :packet-bytes        
    if (:type = 42) [ process-data-packet ] ;42 is the type we defined on the app board
  ]
end

to process-data-packet 
        print :packet-bytes
	make "light wnth 4 :packet-bytes  ;comes from fprint statement on app board
	make "temp1 wnth 5 :packet-bytes  ;comes from fprint statement on app board
	make "temp2 wnth 6 :packet-bytes  ;comes from fprint statement on app board
  	make "time elapsedTime
	print (se "|light:| :light "|temp1:| :temp1 "|temp2:| :temp2 "|time:| :time)
  	addTimeToList :time
 	addLightToList :light 
 	plot-light
end

to plot-light
   x-data "|Elapsed Time (seconds)| :time-list
   y-data "|Light (adc unit)|       :light-list 
   plot 2
   if(:firstrun = 1)
   [
      display 
   ]
  
end

to packet-words :l          
  let [res []]
  let [nws (first :l) / 2 + 2]
  if ( :nws > 25 ) [ make "nws 25 ]
  dotimes [i :nws - 1][make "res se :res bf 100000 + wnth :i :l]
  output :res
end

to wnth :i :l
  output (nth 2 * :i :l) + 256 * (nth 2 * :i + 1 :l)
end

to elapsedTime
   output  int ( now / 1000 ) - :start-time
end

to addTimeToList :time
  make "time-list (se :time-list :time)
end

to addLightToList :photo
  make "light-list (se :light-list :photo)
end

to x-data :n :m
  make "xname :n
  make "xcol-data :m
end

to y-data :n :m
  make "yname :n
  make "ycol-data :m
end

to plot :s
  setup-screen
  clean
  select-all
  limits xlower xupper ylower yupper 
  grid 10 10
  box
  xaxis 10  1
  yaxis 10  1

  if :s = 0 [ histogram ]
  if :s = 1 [ connect ]
  if :s = 2 [ ptype 2 points ]

  title (se :yname "versus :xname)
  xlabel :xname
  redraw

end

to display  
  blindexec "|display -update 1 screen.png | 
  make "firstrun 0 
end

to retrieve-packet
   output rl $1fc0 30 
end